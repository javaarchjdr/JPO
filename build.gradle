buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.0.0'
    }
}

plugins {
    id "org.sonarqube" version "2.7.1"
    id "com.github.ben-manes.versions" version "0.21.0"
    id 'signing'
}

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'java'
apply plugin: "jacoco"
apply plugin: "eclipse"

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {options.encoding = "UTF-8"}
compileTestJava {options.encoding = "UTF-8"}
javadoc {options.encoding = "UTF-8"}

repositories {
    mavenCentral()
}

def jpoVersion = '0.13'

dependencies {
    compile 'org.apache.commons:commons-compress:1.18'
    compile 'org.apache.directory.studio:org.apache.commons.io:2.4'
    compile 'org.apache.commons:commons-jcs-core:2.2.1'
    compile 'org.apache.commons:commons-jcs-jcache:2.2.1'
    compile 'org.apache.commons:commons-lang3:3.9'
    compile 'org.apache.commons:commons-text:1.6'
    compile 'commons-logging:commons-logging:1.2'
    compile 'commons-net:commons-net:3.6'
    compile 'commons-io:commons-io:2.6'
    compile 'com.google.gdata:core:1.47.1'

    compile files('libs/gdata-maps-2.0.jar')
    compile files('libs/gdata-media-1.0.jar')
    compile files('libs/gdata-photos-2.0.jar')
    
    compile 'com.google.guava:guava:27.1-jre'
    compile 'javax.mail:javax.mail-api:1.6.2'
    compile 'com.jcraft:jsch:0.1.55'

    compile files('libs/jwizz-0.1.4.jar')
    
    compile 'org.jxmapviewer:jxmapviewer2:2.4'
    compile 'com.drewnoakes:metadata-extractor:2.11.0'

    compile 'com.miglayout:miglayout-swing:5.2'

    // needed to pull down the August 2018 version from http://www.docking-frames.org/download.html
    //compile group: 'org.dockingframes', name: 'docking-frames-common', version: '1.1.2p6a'
    //compile group: 'org.dockingframes', name: 'docking-frames-core', version: '1.1.2p6a'
    compile files('libs/docking-frames-common.jar')
    compile files('libs/docking-frames-core.jar')

    compile files('libs/TagCloud.jar')
    
    //used by Metadata Extractor. Note only 5.x seems to be compatible
    compile 'com.adobe.xmp:xmpcore:5.1.3'
    //compile 'com.adobe.xmp:xmpcore:6.1.10'
    compile 'org.jetbrains:annotations:17.0.0'

    compile 'com.twelvemonkeys.imageio:imageio-batik:3.4.1'  //OK
    compile 'org.apache.xmlgraphics:batik-transcoder:1.11'
    compile 'org.apache.xmlgraphics:batik-anim:1.11'
    compile 'org.apache.xmlgraphics:batik-rasterizer-ext:1.11'
    compile 'org.apache.xmlgraphics:batik-extension:1.11'
    compile 'org.apache.xmlgraphics:xmlgraphics-commons:2.3'
    compile 'org.apache.xmlgraphics:batik-svggen:1.11'
    compile 'com.twelvemonkeys.imageio:imageio-bmp:3.4.1' //OK
    //compile 'com.twelvemonkeys.imageio:imageio-clippath:3.4.1' //OK -- But what does it do?
    compile 'com.twelvemonkeys.imageio:imageio-icns:3.4.1' //OK
    compile 'com.twelvemonkeys.imageio:imageio-iff:3.4.1' //OK
    compile 'com.twelvemonkeys.imageio:imageio-jpeg:3.4.1' //OK
    compile 'com.twelvemonkeys.imageio:imageio-hdr:3.4.1' //OK
    compile 'com.twelvemonkeys.imageio:imageio-pcx:3.4.1' //OK
    compile 'com.twelvemonkeys.imageio:imageio-pdf:3.4.1'  // doesn't work
    compile 'com.twelvemonkeys.imageio:imageio-pict:3.4.1' //OK
    compile 'com.twelvemonkeys.imageio:imageio-pnm:3.4.1' //OK
    compile 'com.twelvemonkeys.imageio:imageio-psd:3.4.1' //OK
    compile 'com.twelvemonkeys.imageio:imageio-sgi:3.4.1' //OK
    compile 'com.twelvemonkeys.imageio:imageio-tiff:3.4.1' //OK
    compile 'com.twelvemonkeys.imageio:imageio-tga:3.4.1' //OK
    compile 'com.twelvemonkeys.imageio:imageio-thumbsdb:3.4.1' //OK
    testCompile "junit:junit:4.13-beta-3"
}


sourceSets {
    main {
        resources {
            srcDir 'src/images'
            include '**/*.gif'
            include '**/*.jpg'
            
            srcDir 'src/dtd'
            include '**/*.dtd'
            include '**/*.txt'
            include '**/*.css'
            include '**/*.js'
                
            srcDir 'src/main/java/jpo/cache'
            include '**/*.ccf'
        }
    }
    test {
        main
        resources  {
            srcDir 'src/test/images'
            include '**/*.jpg'
            include '**/*.tiff'
            include '**/*.hdr'
            include '**/*.pdf'
            include '**/*.svg'
            include '**/*.bmp'
            include '**/*.pnm'
            include '**/*.sgi'
            include '**/*.tga'
            include '**/*.psd'
            include '**/*.ico'
            include '**/*.png'
            include '**/*.gif'
            include '**/*.iff'
            include '**/*.pcx'
            include '**/*.pct'
            include '**/*.icns'
            include '**/*.db'
        }
    }
}
    

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

jar {
    baseName = 'jpo'
    version =  jpoVersion
    
    manifest {
        attributes(
            "Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' '),
            "Main-Class": 'Main',
            'Permissions': 'all-permissions'
        )
        attributes(
            [ "Copy": "(c) 2001-2019, Richard Eigenmann, ZÃ¼rich, Switzerland" ], "Copyright"
        )
    }
    into ('jpo/images') { from 'src/images' }
}

// this task creates the FatJar with everything in it
shadowJar {
    baseName = 'jpo'
    classifier = 'all'
    version = jpoVersion
    into ('jpo/images') { from 'src/images' }
    mergeServiceFiles()
}



task signJar(dependsOn: 'shadowJar',description: 'Sign shadowJar.',group: 'build'){
    doLast{
        def props = new Properties()
        file("/richi/Privat/Documents/jpo.private.gradle.properties").withInputStream { props.load(it) }
        println 'Keystore file name: ' + props.getProperty("keystoreFileName")

        File signdir  = new File("$buildDir/jars/signed") as File
        signdir.mkdirs()

        ant.signjar(
            destDir: "${signdir.absolutePath}",
                  jar: 'build/libs/jpo-' + jpoVersion + '-all.jar',
                  alias: props.getProperty("keyalias"),
                  storetype: "jks",
                  keystore: props.getProperty("keystoreFileName"),
                  storepass: props.getProperty("keystorepass"),
                  verbose: true,
                  preservelastmodified: "true",
                  tsaurl: "http://timestamp.digicert.com"
      )
    }
}

task buildWindowsExecutable(type: Exec) {
    dependsOn shadowJar
    File exedir  = new File("$buildDir/executables") as File
    exedir.mkdirs()
    commandLine '/usr/bin/makensis', '-XOutFile ' + exedir + '/Jpo.exe', 'Windowsbuilder/jpoexe.nsi'
}

task buildWindowsInstaller(type: Exec) {
    dependsOn buildWindowsExecutable
    File exedir  = new File("$buildDir/executables") as File
    workingDir exedir.toString()
    commandLine '/usr/bin/makensis', 
        '-XOutFile ' + exedir + '/JPO-Installer-0.13.exe', 
        projectDir.absolutePath + '/Windowsbuilder/jpoinstaller.nsi'
}


task run(type: JavaExec) {
    group 'build'
    description 'Runs JPO from the shadowJar'
    dependsOn shadowJar
    classpath = files( 'build/libs/jpo-' + jpoVersion + '-all.jar' )
    main = 'Main'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport


configurations {
  sshAntTask
}

dependencies {
    sshAntTask 'org.apache.ant:ant-jsch:1.10.6', 'com.jcraft:jsch:0.1.55'
}

ant.taskdef(
    name: 'scp',
    classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp',
    classpath: configurations.sshAntTask.asPath)

task uploadWebsitePages() {
    group 'website'
    description 'Publishes the JPO website pages'
    doLast  {
        ant.scp(
            todir: 'richieigenmann,j-po@web.sourceforge.net:/home/groups/j/j-/j-po/htdocs',
            keyfile: '${user.home}/.ssh/id_rsa',
            passphrase: '',
            trust: 'yes',
            verbose: 'yes',
            sftp: 'true', {
                fileset(dir: 'web') {
                    modified()
                    include(name: '*.html')
                    include(name: '*.css')
                    include(name: '*.php')
                    include(name: 'fontconfig')
                    include(name: '*.gif')
                    include(name: '*.png')
                    include(name: '*.jpg')
                    include(name: '*.jnlp')
                    include(name: '*.bat')
                }
            }
        )
    }
}

task uploadWebStartFiles() {
    group 'website'
    description 'Publishes the JPO webstart files'
    dependsOn signJar
    doLast  {
        ant.scp(
            todir: 'richieigenmann,j-po@web.sourceforge.net:/home/groups/j/j-/j-po/htdocs',
            keyfile: '${user.home}/.ssh/id_rsa',
            passphrase: '',
            trust: 'yes',
            verbose: 'yes',
            sftp: 'true', {
                fileset(dir: 'build/jars/signed') {
                    modified()
                    files( 'jpo-' + jpoVersion + '-all.jar' )
                }
                fileset(dir: 'web') {
                    modified()
                    files( 'jpo-' + jpoVersion + '.jnlp' )
                }
            }
        )
    }
}

task uploadJavadoc() {
    group 'website'
    description 'Publishes the JPO website pages'
    dependsOn javadoc
    doLast  {
        ant.scp(
            todir: 'richieigenmann,j-po@web.sourceforge.net:/home/groups/j/j-/j-po/htdocs/',
            keyfile: '${user.home}/.ssh/id_rsa',
            passphrase: '',
            trust: 'yes',
            verbose: 'yes',
            sftp: 'true', {
                fileset(dir: 'build/docs/') {
                    modified()
                }
            }
        )
    }
}

task uploadWindowsInstaller() {
    group 'website'
    description 'Publishes the JPO Windows Installer files'
    dependsOn buildWindowsInstaller
    doLast  {
        ant.scp(
            todir: 'richieigenmann,j-po@web.sourceforge.net:/home/frs/project/j-po/',
            keyfile: '${user.home}/.ssh/id_rsa',
            passphrase: '',
            trust: 'yes',
            verbose: 'yes',
            sftp: 'true', {
                fileset(dir: 'build/executables') {
                    modified()
                    include(name: 'JPO-Installer-' + jpoVersion + '.exe' )
                }
            }
        )
    }
}


task uploadWebsite() {
    group 'website'
    description 'Publishes the JPO website'
    dependsOn uploadWebStartFiles, uploadWebsitePages, uploadJavadoc, uploadWindowsInstaller
}